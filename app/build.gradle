apply plugin: 'com.android.application'
apply plugin: 'com.jaredsburrows.license'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply from: '../env/env.gradle'
apply from: '../utils.gradle'
apply plugin: 'birdflightupload'

licenseReport {
    // To generate the report, run the gradle task 'licenseDevDebugReport'.
    generateHtmlReport = true
    generateJsonReport = false
}

def getGithubSourceCodeZipURL = { ->
    def gitRef = System.getenv("CI_COMMIT_REF_NAME")
    if(!gitRef){
        gitRef = gitBranch()
    }
    def githubUrl = "https://github.com/telekom/syncplus/archive/" + gitRef + ".zip"

    copy {
        from("src/main/assets")
        into("src/main/assets")
        include("osdf.html")
        filter{ it.replaceAll("@@GITHUB_SOURCECODE_ZIP_URL@@", githubUrl)}
        rename { String fileName ->
            fileName.replace("osdf", "osdf-git")
        }
    }

    return githubUrl
}

static def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}


android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    signingConfigs {
        // signing configuration for a local debug.keystore file
        debug {
            storeFile file('../syncplus-debug.keystore')
            storePassword "syncplus"
            keyAlias "syncplus"
            keyPassword "syncplus"
        }
    }

    defaultConfig {
        applicationId "de.telekom.sync.plus"
        minSdkVersion 21    // Android 5
        targetSdkVersion 30 // Android 11
        versionCode 36
        versionName "1.5.1"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "java.util.Map<String, String[]>", "ENVIRON", getEnvHashMapAsString(env)
        resValue "string", "REDIRECT_SCHEME", redirectScheme
        resValue "string", "REDIRECT_URI", redirectUri

        /*builConfigField "String", "{GITHUB_SOURCECODE_ZIP_URL}", getGithubSourceCodeZipURL()*/
        print getGithubSourceCodeZipURL()
    }

    flavorDimensions "environment"
    productFlavors {
        dev {
            applicationIdSuffix ""
            versionNameSuffix "-dev"
        }
        vtu {
            applicationIdSuffix ""
            versionNameSuffix "-vtu"
        }
        beta {
            applicationIdSuffix ""
            versionNameSuffix "-beta"
        }
        prod {
            applicationIdSuffix ""
            versionNameSuffix ""
        }
    }

    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
}

dependencies {
    implementation project(':dtagsyncpluskit')

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.fragment:fragment-ktx:1.3.4'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.github.AppIntro:AppIntro:5.1.0'
    implementation 'com.jaredrummler:material-spinner:1.3.1'
    implementation 'com.github.rahatarmanahmed:circularprogressview:2.5.0'

    def room_version = '2.2.6'
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}


// Configure the extension
birdflightuploadExtension {
    apiKey = birdflightApiKey
    appKey = birdflightAppKey
    id = "syncplus-aos-cicd-" + System.getenv("CI_PIPELINE_ID") + "-" + System.getenv("CI_JOB_ID")
    appVersion = gitBranch()
    compatiblity = "Android"
    notes = System.getenv("CI_COMMIT_REF_NAME") + "\n" + System.getenv("CI_COMMIT_MESSAGE")+ "\n" + System.getenv("CI_COMMIT_SHORT_SHA")
    isPublic = "true"
    httpProxyHost = "proxy.infrastruktur.aul.t-online.de"
    httpProxyPort = 3128
}

