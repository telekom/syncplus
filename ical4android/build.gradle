
buildscript {
    ext.versions = [
        kotlin: '1.3.61',
        dokka: '0.10.0',
        ical4j: '2.2.6'
    ]

    repositories {
        jcenter()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}"
    }
}

repositories {
    jcenter()
    google()
}

apply plugin: 'com.android.library'
apply plugin: 'com.jaredsburrows.license'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka'

licenseReport {
    // To generate the report, run the gradle task 'licenseDevDebugReport'.
    generateHtmlReport = true
    generateJsonReport = true
}


android {
    compileSdkVersion 29
    buildToolsVersion '29.0.2'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "version_ical4j", "\"${versions.ical4j}\""
    }

    lintOptions {
        disable 'AllowBackup'
        disable 'InvalidPackage'
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
    }

    sourceSets {
        main.java.srcDirs = [ "src/main/java", "opentasks-contract/src/main/java" ]
    }

    dokka.configuration {
        sourceLink {
            url = "https://gitlab.com/bitfireAT/ical4android/tree/master/"
            lineSuffix = "#L"
        }
        jdkVersion = 7
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}"

    api "org.mnode.ical4j:ical4j:${versions.ical4j}"
    implementation 'org.slf4j:slf4j-jdk14:1.7.26'
    implementation 'androidx.core:core-ktx:1.2.0'

    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'

    // This very old Groovy version is not actually used, but it's needed
    // so that ical4j doesn't crash when testing: https://github.com/ical4j/ical4j/issues/315
    //noinspection GradleDependency
    androidTestImplementation 'org.codehaus.groovy:groovy:2.2.2'

    testImplementation 'junit:junit:4.12'
}
